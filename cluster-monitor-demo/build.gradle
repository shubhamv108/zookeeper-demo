plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'code.shubham'
version = '1.0.0'

def mainClassName = project.hasProperty("MAIN_CLASS") ? project.getProperty("MAIN_CLASS") : "code.shubham.Main"

jar {
    manifest {
        attributes(
                'Main-Class': mainClassName
        )
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.named('shadowJar') {
    archiveClassifier.set('')
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.apache.zookeeper:zookeeper:3.9.3")

    implementation 'org.slf4j:slf4j-api:2.0.13'
    implementation 'org.apache.logging.log4j:log4j-api:2.20.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.20.0'
    implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.20.0'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}